Constant Story "The Uninvited Opera of Augustus Interruptus";
Constant Headline "^Written and directed by Joshua Wilson^";
Array UUID_ARRAY static string "UUID://b72c87a5-f92e-4f07-b3e7-ba4b5243611a//";
Release 1;

!-----------------------------------------------------------------------------
!
! This file is a test suite for Inform Markup.  For more information on
! Inform Markup, see the header commentary in the file Markup.h.
!
! Here's a suitable PIC1 file for image tests:
!
! begin 664 PIC1
! MB5!.1PT*&@H````-24A$4@```"`````@`@,````.%))G````#%!,5$4```#.
! MSJ/DYOS__]UOK-:X````H$E$051XG'7/L0K",!0%T`NA%+(X.6?O/TBEHXLN
! MG?V4ULG?2+:0KW!S5>N'9.C8<LTK%:KBFPZ\R\T+PCQ8PO^@U7]7I_<JKR;8
! MPCQLD9!MRYA5`=X\F_YX"[`<S9U#`4?NKQPG=&9&+&?TC<"2`R7L&T$O/8+4
! M$W(`*C6'EBFB$UP$-O*H79$7@2/4*#B#@]HMPWY=UX?IYA;07W__Q`NHM)Z\
! 0,;6D%`````!)14Y$KD)@@@``
! `
! end
!
!-----------------------------------------------------------------------------

Constant MAX_CARRIED = 1;
Include "vorple.h";
Include "uoai-parser.h";
Include "uoai-verblib.h";
Include "infglk.h";
Include "markup.h";
Include "uoai-vorple-multimedia.h";
Include "vorple-hyperlinks.h";
Include "vorple-screen-effects.h";

!FIXME Need to set client side boolean on restore if narration is disabled.

[ ClearNarration;
  VorpleExecuteJavaScriptCommand("CancelTTS();");
  VorpleStopMusic();
  VorpleClearPlaylist();
];

[ CompareStrings bufA bufB
    i;
    if (bufA-->0 ~= bufB-->0) rfalse;
    for (i=0 : i<bufA-->0 : i++) {
        if ((bufA->(i+WORDSIZE)) ~= (bufB->(i+WORDSIZE))) rfalse;
      }
    rtrue;
];

[ StringToLower bufA
    i;
    for (i=0 : i<bufA-->0 : i++) {
        if ((bufA->(i+WORDSIZE)) >= 'A' && (bufA->(i+WORDSIZE)) <= 'Z') {
          (bufA->(i+WORDSIZE)) = (bufA->(i+WORDSIZE))-('A' - 'a');
        }
      }
    rtrue;
];


[ BeforeParsing;
  ClearNarration();
];

[ AfterPrompt;
  if (System.audioEnabled == 0) {
    ClearNarration();
  }
  else {
    switch (action) {
      ##Look:
      ##Examine:
      ##Inv:
      ##Go:
      default:
          VorpleExecuteJavaScriptCommand("CancelBin();");
    }
    VorpleExecuteJavaScriptCommand("FlushBinOrTTS();");
  }
];

[ HandleGlkEvent ev context abortres newcmd cmdlen;
  if (isVorpleSupported()) {
  }
  else {
    switch (ev-->0)
    {
    evtype_hyperlink:
      glk_request_hyperlink_event(gg_mainwin);
      glk_set_style(style_Input);
      switch (ev-->2) {
        LINKID_YES: newcmd = "yes";
        LINKID_NO: newcmd = "no";
        LINKID_HERO: newcmd = "Hero";
        LINKID_HEROINE: newcmd = "Heroine";
        LINKID_HERETIC: newcmd = "Heretic";
        LINKID_HELLION: newcmd = "Hellion";
        LINKID_HONEYBADGER: newcmd = "Honeybadger";
        LINKID_CONTINUE: newcmd = "";
        default: newcmd = 0;
      }
      if (newcmd ~= 0) {
    		PrintStringOrArray(newcmd);
    		glk_set_style(style_Normal);
    		glk_cancel_line_event(gg_mainwin,0);
    		cmdlen=PrintAnyToArray(abortres+WORDSIZE,INPUT_BUFFER_LEN-WORDSIZE,newcmd);
    		abortres-->0=cmdlen;
    		return 2;
      }
    }
  }
];

Include "grammar.h";

Constant AUDIOEXT_OGG 0;
Constant AUDIOEXT_MP3 1;
Constant AUDIOEXT_NULL 2;

Array CODENAME_HERO buffer "hero";
Array CODENAME_HEROINE buffer "heroine";
Array CODENAME_HERETIC buffer "heretic";
Array CODENAME_HELLION buffer "hellion";
Array CODENAME_HONEYBADGER buffer "honeybadger";

Constant LINKID_YES 1;
Constant LINKID_NO 2;
Constant LINKID_HERO 3;
Constant LINKID_HEROINE 4;
Constant LINKID_HERETIC 5;
Constant LINKID_HELLION 6;
Constant LINKID_HONEYBADGER 7;
Constant LINKID_CONTINUE 8;

Object System
  with
    audioEnabled 0,
    audioExt AUDIOEXT_NULL,
    FlushBinOrTTSIfNarrationEnabled [;
      if (self.audioEnabled) {
        VorpleExecuteJavaScriptCommand("FlushBinOrTTS();");
      }
    ],
    RestartBackgroundLoop [ f delay;
      VorpleStopAudio();
      if (self.audioEnabled) {
        VorplePlaySoundEffect(f,true,delay);
      }
    ],
    SyncBrowserAudioExtension [;
      switch (self.audioExt) {
        AUDIOEXT_OGG:
          VorpleExecuteJavaScriptCommand("document.audioExt = 'ogg';");
        AUDIOEXT_MP3:
          VorpleExecuteJavaScriptCommand("document.audioExt = 'mp3';");
        default:
          VorpleExecuteJavaScriptCommand("document.audioExt = null;");
      }
    ],
    SyncSystemAudioExtension [ tmp;
      VorpleExecuteJavaScriptCommand("return IsPlayingMP3s();");
      tmp = VorpleWhatNumberWasReturned();
      if (tmp == 1) {
        self.audioExt = AUDIOEXT_MP3;
      }
      else {
        VorpleExecuteJavaScriptCommand("return IsPlayingOGGs();");
        tmp = VorpleWhatNumberWasReturned();
        if (tmp == 1) {
          self.audioExt = AUDIOEXT_OGG;
        }
        else {
          self.audioExt = AUDIOEXT_NULL;
        }
      }
    ],
    StandardPrompt [;
      VorpleExecuteJavaScriptCommand("vorple.prompt.setPrefix('>')");
      bp_output_stream(3, current_prompt, BUFLEN-1);
      print ">";
      bp_output_stream(-3);
    ],
    BlankPrompt [;
      VorpleExecuteJavaScriptCommand("vorple.prompt.setPrefix('')");
      bp_output_stream(3, current_prompt, BUFLEN-1);
      print "";
      bp_output_stream(-3);
    ],
    VorpleClearScreen [;
      ClearScreen();
      VorpleExecuteJavaScriptCommand("$('#window0 *:not(.turn.current), .turn.current').empty()");
    ],
    MsgYesOrNo [;
      print "Please answer ";
      self.InlineLink("yes",LINKID_YES);
      print " or ";
      self.InlineLink("no",LINKID_NO);
      print ".";
    ],
    InlineLink [ text id;
      if (isVorpleSupported()) {
        VorpleLinkCommand(text);
      }
      else if (glk_gestalt(gestalt_Hyperlinks, 0) && id > 0) {
        glk_set_hyperlink(id);
        PrintStringOrArray(text);
        glk_set_hyperlink(0);
      }
      else {
        PrintStringOrArray(text);
      }
    ],
    WaitForEnter [;
      self.BlankPrompt();
      KeyboardPrimitive(buffer, parse);
      self.StandardPrompt();
      return parse;
    ],
    Pause [;
      if (isVorpleSupported()) {
        VorpleImage("continue.png", "CLICK TO CONTINUE", IMAGE_RIGHT_ALIGNED, 0, "");
      }
      else {
        glk_set_hyperlink(LINKID_CONTINUE);
        print (markup) "<IMG SRC=101 ALIGN='RIGHT' ALT='CLICK TO CONTINUE' WIDTH=240 HEIGHT=60><br>";
        glk_set_hyperlink(0);
      }
      self.WaitForEnter();
    ],
    GraceNote [ channel;
      if (isVorpleSupported()) {
        VorpleImage("gracenotes.png", "MUSICAL INTERLUDE");
      }
      else {
        print (markup) "<IMG SRC=100 ALT='MUSICAL INTERLUDE' WIDTH=500 HEIGHT=30><br>";
      }
    ];

[ Initialise o;
    location = room;
    inventory_style = FULLINV_BIT + ENGLISH_BIT + RECURSE_BIT;
    no_implicit_actions = true;
    glk_request_hyperlink_event(gg_mainwin);
    VorpleInitialise();
    objectloop(o provides articles) {
      if (isVorpleSupported() == 0) {
        o.&articles-->0 = "The ";
        o.&articles-->1 = "the ";
        o.&articles-->2 = "a ";
      }
    }
];

[ InteractiveIntroduction tmp v key;
  key = 0;
  System.StandardPrompt();
  if (isVorpleSupported()) {
    VorpleExecuteJavaScriptCommand("return IsTTSSupported();");
    tmp = VorpleWhatNumberWasReturned();
    if (tmp == 1) {
      v = true;
      print "^Would you like to enable audio? ";
      System.MsgYesOrNo();
      System.audioEnabled = YesOrNo();
      if (System.audioEnabled) {
        ClearNarration();
        QueueBinOutput("na_intro_1");
        print "^";
        print "NARRATOR:^";
        print "Oh dear - such dreadful weather.  Do you hear me speaking right now? (";
        System.InlineLink("yes",LINKID_YES);
        print " or ";
        System.InlineLink("no",LINKID_NO);
        print ")^";
        System.FlushBinOrTTSIfNarrationEnabled();
        tmp = YesOrNo();
        if (tmp) {
          System.SyncSystemAudioExtension();
        }
        else {
          VorpleExecuteJavaScriptCommand("ToggleAudioExtension();");
          ClearNarration();
          QueueBinOutput("na_intro_1b");
          print "^";
          print "Oh, how about now then? (";
          System.InlineLink("yes",LINKID_YES);
          print " or ";
          System.InlineLink("no",LINKID_NO);
          print ")^";
          System.FlushBinOrTTSIfNarrationEnabled();
          tmp = YesOrNo();
          if (tmp) {
            System.SyncSystemAudioExtension();
          }
        }
        if (tmp) {
          ClearNarration();
          QueueBinOutput("na_intro_1c");
          QueueBinOutput("se_pause");
          QueueBinOutput("na_intro_1d");
          QueueBinOutput("se_pause");
          print "^";
          print "If you see the continue symbol or hear the report of musical drums...you should press the enter key or tap continue when ready to proceed.^";
          System.FlushBinOrTTSIfNarrationEnabled();
          System.Pause();
          System.VorpleClearScreen();
          ClearNarration();
          QueueBinOutput("na_intro_2");
          QueueBinOutput("se_pause");
          print "^";
          print "On occasion, the game may employ miraculous text-to-speech technology to read some words aloud.^";
          System.FlushBinOrTTSIfNarrationEnabled();
          System.Pause();
          System.VorpleClearScreen();
          ClearNarration();
          print "^This is some sample text-to-speech.  Would you like to customize the voice? (";
          System.InlineLink("yes",LINKID_YES);
          print " or ";
          System.InlineLink("no",LINKID_NO);
          print ")^";
          System.FlushBinOrTTSIfNarrationEnabled();
          tmp = YesOrNo();
          if (tmp) {
            ClearNarration();
            print "^So would I.  Alas, not today.";
            PunctuateTTS();
            System.FlushBinOrTTSIfNarrationEnabled();
            key = 1;
          }
          else {
            ClearNarration();
            QueueBinOutput("na_intro_3");
            QueueBinOutput("se_pause");
            print "^";
            print "Excellent! Press enter to begin the game.^";
            System.FlushBinOrTTSIfNarrationEnabled();
            key = 1;
          }
        }
        else {
          ClearNarration();
          print "Never mind about the audio then.";
          key = 1;
          System.audioEnabled = 0;
        }
      }
    }
    else {
      v = false;
    }
  }
  else {
    v = false;
  }
  if (v == false) {
    print "^NARRATOR:^";
    print "Please note that there will be no audio playing, as this interpreter does not support it.  When you see the continue symbol, tap it or press enter to proceed.  Enjoy the game!^^";
    key = 1;
  }
  if (key == 1) {
    System.Pause();
  }
  ClearNarration();
  VorpleStopAudio();
  System.VorpleClearScreen();

  !FIXME Improve concision and humor
  if (isVorpleSupported()) {
    VorpleImage("title.png", "The Uninvited Opera Of Augustus Interruptus");
  }
  else {
    print (markup) "<IMG SRC=102 ALT='The Uninvited Opera of Augustus Interruptus'<br>";
    print "^";
  }

  QueueBinOutput("na_prologue_1");
  print "NARRATOR:^";
  print "Your people have known nothing but tyranny for one thousand years.  Healthcare is free, yes, but only for ones who are sick! Pointless war and carnage is long forbidden, blanching the countryside with the sickly pallor of peace and prosperity.  And worst of all, excessive overdue library fines - pennies per day! All that changes tonight, and you shall be the changer!^";
  print "^Yet no one shall ever know your name.  Indeed, if you even have a true name it is lost to time.  You were raised by the Order of Humble Pride, a cabal of freedom fighters masquerading as learned monks.  'Twas twenty-four years ago they found you abandoned at the gates and assigned to you a random nom de guerre - a code name otherwise known only to the elders.^";
  print "^What was it again? Something that started with H I believe. ";
  System.InlineLink("Hero",LINKID_HERO);
  print "? ";
  System.InlineLink("Heroine",LINKID_HEROINE);
  print "? ";
  System.InlineLink("Heretic",LINKID_HERETIC);
  print "? ";
  System.InlineLink("Hellion",LINKID_HELLION);
  print "? ";
  print "Or perhaps ";
  System.InlineLink("Honeybadger",LINKID_HONEYBADGER);
  print "? You can tell me.  I'm the narrator, after all.^";
  System.FlushBinOrTTSIfNarrationEnabled();
  if (true) {
  tmp = false;
  while (tmp == false) {
    KeyboardPrimitive(buffer, parse);
    StringToLower(buffer);

    if (CompareStrings(buffer,CODENAME_HERO) || CompareStrings(buffer,CODENAME_HEROINE)) {
      tmp = true;
    }
    else if (CompareStrings(buffer,CODENAME_HERETIC)) {
      tmp = true;
    }
    else if (CompareStrings(buffer,CODENAME_HELLION)) {
      tmp = true;
    }
    else if (CompareStrings(buffer,CODENAME_HONEYBADGER)) {
      tmp = true;
    }
    else {
      ClearNarration();
      QueueBinOutput("na_prologue_1_rejection");
      print "^No no no, it can't be that.  Please enter ";
      System.InlineLink("Hero",LINKID_HERO);
      print ", ";
      System.InlineLink("Heroine",LINKID_HEROINE);
      print ", ";
      System.InlineLink("Heretic",LINKID_HERETIC);
      print ", ";
      System.InlineLink("Hellion",LINKID_HELLION);
      print ", or ";
      System.InlineLink("Honeybadger",LINKID_HONEYBADGER);
      print ".^";
      System.FlushBinOrTTSIfNarrationEnabled();
    }
  }
  System.RestartBackgroundLoop("music_loop_1.ogg",1000);
  ClearNarration();
  QueueBinOutput("na_prologue_1_ahyes");
  print "^Ah yes - ";
  if (CompareStrings(buffer,CODENAME_HERO) || CompareStrings(buffer,CODENAME_HEROINE)) {
    if (CompareStrings(buffer,CODENAME_HERO)) {
      QueueBinOutput("na_prologue_1_hero");
      print "Hero! ";
    }
    else {
      QueueBinOutput("na_prologue_1_heroine");
      print "Heroine! ";
    }
    QueueBinOutput("na_prologue_1_hero_fate");
    QueueBinOutput("se_pause");
    print "Destined to save your nation and change the world!^";
    System.FlushBinOrTTSIfNarrationEnabled();
  }
  else if (CompareStrings(buffer,CODENAME_HERETIC)) {
    QueueBinOutput("na_prologue_1_heretic");
    QueueBinOutput("na_prologue_1_heretic_fate");
    QueueBinOutput("se_pause");
    print "Heretic! Ever ready to trample the trappings of fear and falsehood beneath your two bare feet.^";
    System.FlushBinOrTTSIfNarrationEnabled();
  }
  else if (CompareStrings(buffer,CODENAME_HELLION)) {
    QueueBinOutput("na_prologue_1_hellion");
    QueueBinOutput("na_prologue_1_hellion_fate");
    QueueBinOutput("se_pause");
    print "Hellion! Angrily spitting in the face of fickle fate!^";
    System.FlushBinOrTTSIfNarrationEnabled();
  }
  else if (CompareStrings(buffer,CODENAME_HONEYBADGER)) {
    QueueBinOutput("na_prologue_1_honeybadger");
    QueueBinOutput("na_prologue_1_honeybadger_fate");
    QueueBinOutput("se_pause");
    print "Honeybadger! The soul of a wolverine in the body of a...wolverinish...badger body!^";
    System.FlushBinOrTTSIfNarrationEnabled();
  }
  }
  System.Pause();
  ClearNarration();
  VorpleStopAudio();
  System.VorpleClearScreen();
];


[ QueueBinOutput f;
  if (isVorpleSupported()) {
    print "=";
    PrintStringOrArray(f);
    print ";";
  }
];

[ PunctuateTTS;
  if (isVorpleSupported()) {
    print "=!;^";
  }
];


Object room "Cell"
  with
    description [;
      QueueBinOutput("na_desc_cell");
      QueueBinOutput("0_25_silence");
      "You are standing in a cell.  The only way out is down.";
    ],
    before [;
      Go:
        if (noun == d_obj) {
          if (System.audioEnabled) {
            VorpleExecuteJavaScriptCommand("PlayNowAsList('na_msg_game_over');");
          }
          print "Indeed, for you will not go gently into that good night.  Game over, for now.";
          KeyCharPrimitive();
          quit;
        }
    ],
  has light;

Object -> cube "steel cube"
  with
    name 'steel' 'cube',
    articles "=na_name_the_cube;The " "=na_name_the_cube;the " "=na_name_a_cube;a ",
    short_name "steel cube",
    description [;
      QueueBinOutput("na_desc_cube");
      "A solid block of mistreated metal.";
    ];

Object -> pistol "strange pistol"
  with
    name 'strange' 'pistol',
    articles "=na_name_the_pistol;The " "=na_name_the_pistol;the " "=na_name_a_pistol;a ",
    short_name "strange pistol",
    description [;
      QueueBinOutput("na_desc_pistol");
      "There's no trigger.  You can't tell how to fire it.";
    ],
  has clothing;
