Constant Story "The Uninvited Opera of Augustus Interruptus";
Constant Headline "^Written and directed by Joshua Wilson^";
Array UUID_ARRAY static string "UUID://b72c87a5-f92e-4f07-b3e7-ba4b5243611a//";
Release 1;

!-----------------------------------------------------------------------------
!
! This file is a test suite for Inform Markup.  For more information on
! Inform Markup, see the header commentary in the file Markup.h.
!
! Here's a suitable PIC1 file for image tests:
!
! begin 664 PIC1
! MB5!.1PT*&@H````-24A$4@```"`````@`@,````.%))G````#%!,5$4```#.
! MSJ/DYOS__]UOK-:X````H$E$051XG'7/L0K",!0%T`NA%+(X.6?O/TBEHXLN
! MG?V4ULG?2+:0KW!S5>N'9.C8<LTK%:KBFPZ\R\T+PCQ8PO^@U7]7I_<JKR;8
! MPCQLD9!MRYA5`=X\F_YX"[`<S9U#`4?NKQPG=&9&+&?TC<"2`R7L&T$O/8+4
! M$W(`*C6'EBFB$UP$-O*H79$7@2/4*#B#@]HMPWY=UX?IYA;07W__Q`NHM)Z\
! 0,;6D%`````!)14Y$KD)@@@``
! `
! end
!
!-----------------------------------------------------------------------------

Constant MAX_CARRIED = 1;
Include "vorple.h";
Include "uoai-parser.h";
Include "uoai-verblib.h";
Include "markup.h";
Include "vorple-multimedia.h";
Include "vorple-hyperlinks.h";
Include "vorple-screen-effects.h";

!FIXME Need to set client side boolean on restore if narration is disabled.

[ ClearNarration;
  VorpleExecuteJavaScriptCommand("CancelTTS();");
  VorpleStopMusic();
  VorpleClearPlaylist();
];

[ BeforeParsing;
  ClearNarration();
];

[ AfterPrompt;
  if (System.audioEnabled == 0) {
    ClearNarration();
  }
  else {
    switch (action) {
      ##Look:
      ##Examine:
      ##Inv:
      ##Go:
      default:
          VorpleExecuteJavaScriptCommand("CancelBin();");
    }
    VorpleExecuteJavaScriptCommand("FlushBinOrTTS();");
  }
];

Include "grammar.h";

Constant AUDIOEXT_OGG 0;
Constant AUDIOEXT_MP3 1;
Constant AUDIOEXT_NULL 2;

Constant CODENAME_HERO 'hero';
Constant CODENAME_HEROINE 'heroine';
Constant CODENAME_HERETIC 'heretic';
Constant CODENAME_HELLION 'hellion';
Constant CODENAME_HONEYBADGER 'honeybadger';

Object System
  with
    audioEnabled 0,
    audioExt AUDIOEXT_NULL,
    FlushBinOrTTSIfNarrationEnabled [;
      if (self.audioEnabled) {
        VorpleExecuteJavaScriptCommand("FlushBinOrTTS();");
      }
    ],
    RestartBackgroundLoop [ f;
      VorpleStopAudio();
      if (self.audioEnabled) {
        VorplePlaySoundEffect(f,true);
      }
    ],
    SyncBrowserAudioExtension [;
      switch (self.audioExt) {
        AUDIOEXT_OGG:
          VorpleExecuteJavaScriptCommand("document.audioExt = 'ogg';");
        AUDIOEXT_MP3:
          VorpleExecuteJavaScriptCommand("document.audioExt = 'mp3';");
        default:
          VorpleExecuteJavaScriptCommand("document.audioExt = null;");
      }
    ],
    SyncSystemAudioExtension [ tmp;
      VorpleExecuteJavaScriptCommand("return IsPlayingMP3s();");
      tmp = VorpleWhatNumberWasReturned();
      if (tmp == 1) {
        self.audioExt = AUDIOEXT_MP3;
      }
      else {
        VorpleExecuteJavaScriptCommand("return IsPlayingOGGs();");
        tmp = VorpleWhatNumberWasReturned();
        if (tmp == 1) {
          self.audioExt = AUDIOEXT_OGG;
        }
        else {
          self.audioExt = AUDIOEXT_NULL;
        }
      }
    ],
    VorpleClearScreen [;
      ClearScreen();
      VorpleExecuteJavaScriptCommand("$('#window0 *:not(.turn.current), .turn.current').empty()");
    ],
    MsgYesOrNo [;
      print "Please answer ";
      VorpleLinkCommand("yes");
      print " or ";
      VorpleLinkCommand("no");
      print ".";
    ],
    WaitForEnter [;
      KeyboardPrimitive(buffer, parse);
    ],
    GraceNote [ channel;
      if (isVorpleSupported()) {
        VorpleImage("gracenotes.png", "MUSICAL INTERLUDE");
      }
      else {
        print (markup) "<IMG SRC=100 ALT='MUSICAL INTERLUDE' WIDTH=500 HEIGHT=30><br>";
      }
    ];

[ Initialise o;
    location = room;
    inventory_style = FULLINV_BIT + ENGLISH_BIT + RECURSE_BIT;
    no_implicit_actions = true;
    VorpleInitialise();
    objectloop(o provides articles) {
      if (isVorpleSupported() == 0) {
        o.&articles-->0 = "The ";
        o.&articles-->1 = "the ";
        o.&articles-->2 = "a ";
      }
    }
];

[ InteractiveIntroduction tmp v key;
  key = 0;
  if (isVorpleSupported()) {
    VorpleExecuteJavaScriptCommand("return IsTTSSupported();");
    tmp = VorpleWhatNumberWasReturned();
    if (tmp == 1) {
      v = true;
      print "^Would you like to enable audio? ";
      System.MsgYesOrNo();
      System.audioEnabled = YesOrNo();
      if (System.audioEnabled) {
        ClearNarration();
        VorpleExecuteJavaScriptCommand("PlayNowAsList('na_intro_1');");
        print "^NARRATOR:^";
        print "Oh dear - such dreadful weather.  Do you hear me speaking right now? (";
        VorpleLinkCommand("yes");
        print " or ";
        VorpleLinkCommand("no");
        print ")";
        tmp = YesOrNo();
        if (tmp) {
          System.SyncSystemAudioExtension();
        }
        else {
          VorpleExecuteJavaScriptCommand("ToggleAudioExtension();");
          ClearNarration();
          VorpleExecuteJavaScriptCommand("PlayNowAsList('na_intro_1b');");
          print "^NARRATOR:^";
          print "Oh, how about now then? (";
          VorpleLinkCommand("yes");
          print " or ";
          VorpleLinkCommand("no");
          print ")";
          tmp = YesOrNo();
          if (tmp) {
            System.SyncSystemAudioExtension();
          }
        }
        if (tmp) {
          ClearNarration();
          print "^On occasion, the game may employ miraculous text-to-speech technology to read some words aloud.  Press enter to hear a sample.^";
          VorpleExecuteJavaScriptCommand("PlayNowAsList('na_intro_2');");
          System.WaitForEnter();
          ClearNarration();
          print "^This is some sample text-to-speech.  Would you like to customize the voice? (";
          VorpleLinkCommand("yes");
          print " or ";
          VorpleLinkCommand("no");
          print ")^";
          System.FlushBinOrTTSIfNarrationEnabled();
          tmp = YesOrNo();
          if (tmp) {
            ClearNarration();
            print "^So would I.  Alas, not today.  Press enter to start the game.^";
            System.FlushBinOrTTSIfNarrationEnabled();
            key = 1;
          }
          else {
            ClearNarration();
            VorpleExecuteJavaScriptCommand("PlayNowAsList('na_intro_3');");
            print "^Excellent! Press enter to begin the game.^";
            key = 1;
          }
        }
        else {
          ClearNarration();
          print "Never mind about the audio then.  Press enter to begin the game!";
          key = 1;
          System.audioEnabled = 0;
        }
      }
    }
    else {
      v = false;
    }
  }
  else {
    v = false;
  }
  if (v == false) {
    print "^NARRATOR:^";
    print "Please note that there will be no audio playing, as this interpreter does not support it.  Press a key to begin the game!";
    key = 1;
  }
  if (key == 1) {
    System.WaitForEnter();
  }
  ClearNarration();
  System.VorpleClearScreen();

  !FIXME Improve concision and humor
  !QueueBinOutput("na_prologue_1");
  print "Your people have known nothing but tyranny for one thousand years.  Healthcare is free, yes, but only for ones who are sick! Pointless war and carnage is long forbidden, blanching the countryside with the sickly pallor of peace and prosperity.  And worst of all, excessive overdue library fines - pennies per day! All that changes tonight, and you shall be the changer!^";
  print "^Yet no one shall ever know your name.  Indeed, if you even have a true name it is lost to time.  You were raised by the Order of Humble Pride, a cabal of freedom fighters masquerading as learned monks.  'Twas twenty-four years ago they found you abandoned at the gates and assigned to you a random nom de guerre - a code name otherwise known only to the elders.^";
  print "^What was it again? Something that started with H I believe. Hero? Heroine? Heretic? Hellion? Or perhaps Honeybadger? You can tell me.  I'm the narrator, after all.^";
  System.FlushBinOrTTSIfNarrationEnabled();
  System.WaitForEnter();
  ClearNarration();
  quit;
];


[ QueueBinOutput f;
  if (isVorpleSupported()) {
    print "=";
    PrintStringOrArray(f);
    print ";";
  }
];

Object room "Cell"
  with
    description [;
      QueueBinOutput("na_desc_cell");
      QueueBinOutput("0_25_silence");
      "You are standing in a cell.  The only way out is down.";
    ],
    before [;
      Go:
        if (noun == d_obj) {
          if (System.audioEnabled) {
            VorpleExecuteJavaScriptCommand("PlayNowAsList('na_msg_game_over');");
          }
          print "Indeed, for you will not go gently into that good night.  Game over, for now.";
          KeyCharPrimitive();
          quit;
        }
    ],
  has light;

Object -> cube "steel cube"
  with
    name 'steel' 'cube',
    articles "=na_name_the_cube;The " "=na_name_the_cube;the " "=na_name_a_cube;a ",
    short_name "steel cube",
    description [;
      QueueBinOutput("na_desc_cube");
      "A solid block of mistreated metal.";
    ];

Object -> pistol "strange pistol"
  with
    name 'strange' 'pistol',
    articles "=na_name_the_pistol;The " "=na_name_the_pistol;the " "=na_name_a_pistol;a ",
    short_name "strange pistol",
    description [;
      QueueBinOutput("na_desc_pistol");
      "There's no trigger.  You can't tell how to fire it.";
    ],
  has clothing;
