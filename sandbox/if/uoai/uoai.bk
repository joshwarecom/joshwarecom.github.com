Constant Story "The Uninvited Epic of Augustus Interruptus";
Constant Headline "^Written and directed by Joshua Wilson^";
Array UUID_ARRAY static string "UUID://b72c87a5-f92e-4f07-b3e7-ba4b5243611a//";
Release 1;

Attribute custom_listing;
!Global TEST_MODE = true;

Constant MAX_CARRIED = 2;
Include "vorple.h";
Include "uoai-parser.h";
Include "uoai-verblib.h";
Include "infglk.h";
Include "markup.h";
Include "uoai-vorple-multimedia.h";
Include "vorple-hyperlinks.h";
Include "vorple-command-prompt-control.h";
Include "vorple-screen-effects.h";

[ ChooseObjects obj code
  retval;
  obj = obj;          ! Avoid a compiler warning
  switch (code) {
      0:              ! Parser is excluding obj from ALL
          ;           ! ... accept parser's decision
      1:              ! Parser is including obj in ALL
          if (action_to_be == ##Take || action_to_be == ##Drop) retval = 2;
                      ! ... force exclusion if TAKE; accept otherwise
      2:              ! Parser is asking for 'appropriateness' hint
          ;           ! ... but we don't provide one
      }
  return retval;
];

[ AfterRestore;
  ClearNarration();
  VorpleStopAudio();
  System.SyncBrowserAudioExtension();
  QueueBinOutput("na_msg_restoresuccessful");
  QueueBinOutput("se_pause");
  print "Restore successful!^";
  System.FlushBinOrTTSIfNarrationEnabled();
  System.Pause();
  ClearNarration();
  VorpleStopAudio();
  System.previousExaminedItem = 0;
  System.previouslyLooked = 0;
];

[ ClearNarration;
  VorpleExecuteJavaScriptCommand("CancelTTS();");
  VorpleStopMusic();
  VorpleClearPlaylist();
];

[ CompareStrings bufA bufB
    i;
    if (bufA-->0 ~= bufB-->0) rfalse;
    for (i=0 : i<bufA-->0 : i++) {
        if ((bufA->(i+WORDSIZE)) ~= (bufB->(i+WORDSIZE))) rfalse;
      }
    rtrue;
];

[ StringToLower bufA
    i;
    for (i=0 : i<bufA-->0 : i++) {
        if ((bufA->(i+WORDSIZE)) >= 'A' && (bufA->(i+WORDSIZE)) <= 'Z') {
          (bufA->(i+WORDSIZE)) = (bufA->(i+WORDSIZE))-('A' - 'a');
        }
      }
    rtrue;
];

!FIXME undo can cause currentCmdSupportsPlayback field to be 0 or 1, resulting in TTS playback of room name.
[ GamePreRoutine tmp;
  System.currentCmdSupportsPlayback = 1;
  if (action == ##Examine) {
    if (System.previousExaminedItem)
      if (System.previousExaminedItem == noun)
        if (noun ofclass HyperLinkedObject) {
          action = ##Interact;
        }
        else {}
      else
        System.previousExaminedItem = noun;
    else
      System.previousExaminedItem = noun;
  }

  switch (action) {
    ##Look:
    ##Examine:
    ##Search:
    ##UOAIInv:
    ##Go:
    ##Open:
    ##Close:
    default:
      System.currentCmdSupportsPlayback = 0;
  }
  return false;
];

[ CopyStringTo bufA bufB
    j;
    for (j=0 : j<bufA-->0 : j++) {
      bufB-->j = bufA-->j;
    }
    rtrue;
];

[ BeforeParsing;
  ClearNarration();
];

[ AfterPrompt;
  if (System.audioEnabled == 0) {
    ClearNarration();
  }
  else {
    if (~~VorpleIsASoundEffectPlaying()) {
      System.RestartBackgroundLoop();
    }
    System.FlushBinOrTTSIfNarrationEnabled();
  }
  if (parent(convenience) == parent(player) && convenience.state == 0) {
    remove convenience;
  }
];

[ HandleGlkEvent ev context abortres newcmd cmdlen;
  if (isVorpleSupported()) {
  }
  else {
    switch (ev-->0)
    {
    evtype_hyperlink:
      glk_request_hyperlink_event(gg_mainwin);
      if (ev-->2 >= CSLINKID_MIN && ev-->2 <= CSLINKID_MAX) {
        System.clickedContextSensitiveLink = true;
        bp_output_stream(3, longstr, LEN_SHORTSTR);
        print (address)System.currentCommandObject.GetContextSensitiveCommandWord(ev-->2);
        if (System.currentCommandObject) {
          print " ";
          System.currentCommandObject.short_name(true);
        }
        bp_output_stream(-3);
        newcmd = longstr;
        switch (ev-->2) {
          CSLINKID_INSPECT:
          CSLINKID_TAKE:
          CSLINKID_DROP:
          CSLINKID_OPEN:
          CSLINKID_CLOSE:
          CSLINKID_WEAR:
          CSLINKID_REMOVE:
          CSLINKID_EMPTY:
          default: newcmd = 0;
        }
      }
      else {
        switch (ev-->2) {
          LINKID_YES: newcmd = "Yes";
          LINKID_NO: newcmd = "No";
          LINKID_HERO: newcmd = "Hero";
          LINKID_HEROINE: newcmd = "Heroine";
          LINKID_HERETIC: newcmd = "Heretic";
          LINKID_HELLION: newcmd = "Hellion";
          LINKID_HONEYBADGER: newcmd = "Honeybadger";
          LINKID_CONTINUE: newcmd = "";
          LINKID_NEW: newcmd = "New";
          LINKID_RESTORE: newcmd = "Restore";
          LINKID_COLOR: newcmd = "Color";
          LINKID_CLOAK: newcmd = "examine woven woolen cloak";
          LINKID_HARNESS: newcmd = "examine supple satin lined weapons harness";
          LINKID_SATCHEL: newcmd = "examine slim leather satchel";
          LINKID_TRUNCHEON: newcmd = "examine trusty truncheon";
          LINKID_KNIFE: newcmd = "examine needle pointed knife";
          LINKID_HOOD: newcmd = "examine hood";
          LINKID_PELLET: newcmd = "examine grim green poison pellet";
          LINKID_FAEFLASK: newcmd = "examine glaes faeflask";
          LINKID_CHARGE: newcmd = "examine peril powder charge";
          LINKID_SHOULDERS: newcmd = "examine my shoulders";
          LINKID_FACE: newcmd = "examine my face";
          LINKID_BODY: newcmd = "examine my body";
          LINKID_BACK: newcmd = "examine my back";
          LINKID_HOOD: newcmd = "examine hood";
          LINKID_CARD: newcmd = "examine laminated parchment card";
          LINKID_AUGUSTUS: newcmd = "examine Augustus";
          LINKID_WATCHMAN: newcmd = "examine watchman";
          LINKID_OWTOWER: newcmd = "examine outer watch tower";
          LINKID_TREES: newcmd = "examine trees";
          LINKID_SOUTH: newcmd = "go south";
          LINKID_INVENTORY: newcmd = "inventory";
          LINKID_PATH: newcmd = "examine path";
          LINKID_CHILL: newcmd = "examine chill";
          LINKID_LUTE: newcmd = "examine lute";
          LINKID_MENU: newcmd = "menu";
          LINKID_LOOK: newcmd = "look";
          default: newcmd = 0;
        }
      }
      if (newcmd ~= 0) {
        glk_set_style(style_Input);
    		PrintStringOrArray(newcmd);
    		glk_set_style(style_Normal);
    		glk_cancel_line_event(gg_mainwin,0);
    		cmdlen=PrintAnyToArray(abortres+WORDSIZE,INPUT_BUFFER_LEN-WORDSIZE,newcmd);
    		abortres-->0=cmdlen;
    		return 2;
      }
    }
  }
];

Include "grammar.h";
Include "uoai-alchemy.inf";

Extend 'inventory' replace
  *                                           -> UOAIInv
  * 'tall'                                    -> EasterEgg
  * 'wide'                                    -> EasterEgg;

Verb 'menu'
  * -> Interact
  * noun -> Interact;

[ InteractSub;
  if (noun && noun ofclass HyperLinkedObject) {
    print "You might try: ";
    if (noun provides ListCommands) {
      noun.ListCommands();
    }
  }
  else if (noun) {
    "There is no menu of suggestions for ", (the)noun;
  }
  else {
    print "You might try: ";
    System.InlineLink("look", LINKID_LOOK );
    print " , ";
    System.InlineLink("inventory", LINKID_INVENTORY);
  }
];

Verb 'inspect'
  * noun -> Inspect;
[ InspectSub;
  ExamineSub();
];


[ IsHood;
  return (noun == hood && TestScope(player,hood));
];
Extend 'wear' first
    * noun=IsHood                               -> Wear;
Extend 'take'
    * 'off' noun=IsHood                         -> Disrobe;
Extend 'disrobe'
    * noun=IsHood                               -> Disrobe;
Extend 'remove' first
    * noun=IsHood                               -> Disrobe;

[ PlayerAddToScope;
  PlaceInScope(shoulders);
  PlaceInScope(face);
  PlaceInScope(body);
  PlaceInScope(back);
  PlaceInScope(narrator);
  PlaceInScope(supplies);
  PlaceInScope(chill);
];

[ GetFirstListedInventory o;
  objectloop(o in actor) {
    if (o hasnt custom_listing) return o;
  }
  return 0;
];

[ InvHarnessContents;
  if (truncheon in harness && knife in harness) {
    QueueBinOutput("na_msg_secured_truncheon_knife");
    print " which secures a ", (name)truncheon, " and a ", (name)knife, " tightly at the small of your ";
    back.short_name();
    print ".  ";
  }
  else if (truncheon in harness) {
    QueueBinOutput("na_msg_secured_truncheon");
    print " which secures a ", (name)truncheon, " tightly at the small of your ";
    back.short_name();
    print ".  ";
  }
  else if (knife in harness) {
    QueueBinOutput("na_msg_secured_knife");
    print " which secures a ", (name)knife, " tightly at the small of your ";
    back.short_name();
    print ".  ";
  }
  else {
    QueueBinOutput("na_msg_harness_empty");
    print ".  The harness is empty.  ";
  }
];
[ InvDescribeHood;
  if (cloak has general) {
    QueueBinOutput("na_msg_hood_on");
    print "The ";
    hood.short_name();
    print " of the ";
    cloak.short_name();
    print " is pulled low over your ";
    face.short_name();
    print ", obscuring your features.  ";
  }
  else {
    QueueBinOutput("na_msg_hood_off");
    print "The ";
    hood.short_name();
    print " of the ";
    cloak.short_name();
    print " hangs down around your ";
    shoulders.short_name();
    print ", revealing your ";
    face.short_name();
    print " to the world.  ";
  }
];

[ UOAIInvSub output harnessDescribed cloakDescribed satchelDescribed;
  if (harness in actor) {
    if (harness has worn) {
      if (cloak in actor && cloak has worn) {
        QueueBinOutput("na_msg_harness_concealed");
        print "A ", (name)cloak, " conceals a ", (name)harness;
        harnessDescribed = 1;
        cloakDescribed = 1;
        InvHarnessContents();
        InvDescribeHood();
      }
      else {
        QueueBinOutput("na_msg_harness_visible");
        harnessDescribed = 1;
        print "You wear a ", (name)harness;
        InvHarnessContents();
      }
    }
  }
  if (cloakDescribed ~= 1) {
    if (cloak in actor) {
      if (cloak has worn) {
        QueueBinOutput("na_msg_cloak_on");
        print "You are wearing a ";
        cloak.short_name();
        print ".  ";
        cloakDescribed = 1;
        InvDescribeHood();
      }
    }
  }
  if (satchel in actor) {
    if (satchel has worn) {
      QueueBinOutput("na_msg_satchel_on");
      print "A ", (name)satchel, " hangs loosely across your ";
      body.short_name();
      print " from your left ";
      System.InlineLinkText("examine my shoulders","shoulder",LINKID_SHOULDERS);
      print ".";
      satchelDescribed = 1;
    }
  }
  if (harnessDescribed || cloakDescribed || satchelDescribed) {
    print "^^";
  }
  if (harness hasnt worn) give harness ~custom_listing;
  if (cloak hasnt worn) give cloak ~custom_listing;
  if (satchel hasnt worn) give satchel ~custom_listing;
  InvSub();
  if (harness hasnt worn) give harness custom_listing;
  if (cloak hasnt worn) give cloak custom_listing;
  if (satchel hasnt worn) give satchel custom_listing;
  System.FlushBinOrTTSIfNarrationEnabled();
];

[ EasterEggSub;
  System.codeNameLinkId = LINKID_HACKER;
  "You discovered the super secret command! I hereby dub thee ~Hacker~.^";
];


Constant AUDIOEXT_OGG 0;
Constant AUDIOEXT_MP3 1;
Constant AUDIOEXT_NULL 2;

Array INPUT_QUIT buffer "quit";
Array INPUT_NEW buffer "new";
Array INPUT_RESTART buffer "restart";
Array INPUT_RESTORE buffer "restore";
Array INPUT_COLOR buffer "color";

Array CODENAME_HERO buffer "hero";
Array CODENAME_HEROINE buffer "heroine";
Array CODENAME_HERETIC buffer "heretic";
Array CODENAME_HELLION buffer "hellion";
Array CODENAME_HONEYBADGER buffer "honeybadger";
Array CODENAME_HACKER buffer "hacker";

Constant LINKID_YES 1;
Constant LINKID_NO 2;
Constant LINKID_HERO 3;
Constant LINKID_HEROINE 4;
Constant LINKID_HERETIC 5;
Constant LINKID_HELLION 6;
Constant LINKID_HONEYBADGER 7;
Constant LINKID_CONTINUE 8;
Constant LINKID_NEW 9;
Constant LINKID_RESTORE 10;
Constant LINKID_COLOR 11;
Constant LINKID_HACKER 12;
Constant LINKID_CLOAK 13;
Constant LINKID_HARNESS 14;
Constant LINKID_SATCHEL 15;
Constant LINKID_TRUNCHEON 16;
Constant LINKID_KNIFE 17;
Constant LINKID_HOOD 18;
Constant LINKID_PELLET 19;
Constant LINKID_FAEFLASK 20;
Constant LINKID_CHARGE 21;
Constant LINKID_SHOULDERS 22;
Constant LINKID_FACE 23;
Constant LINKID_BODY 24;
Constant LINKID_BACK 25;
Constant LINKID_CARD 26;
Constant LINKID_AUGUSTUS 27;
Constant LINKID_WATCHMAN 28;
Constant LINKID_OWTOWER 29;
Constant LINKID_TREES 30;
Constant LINKID_SOUTH 31;
Constant LINKID_INVENTORY 32;
Constant LINKID_PATH 33;
Constant LINKID_CHILL 34;
Constant LINKID_LUTE 35;
Constant LINKID_MENU 36;
Constant LINKID_LOOK 37;

Constant CSLINKID_MIN 1001;
Constant CSLINKID_MAX 1999;
Constant CSLINKID_INSPECT 1001;
Constant CSLINKID_TAKE 1002;
Constant CSLINKID_DROP 1003;
Constant CSLINKID_OPEN 1004;
Constant CSLINKID_CLOSE 1005;
Constant CSLINKID_WEAR 1006;
Constant CSLINKID_REMOVE 1007;
Constant CSLINKID_EMPTY 1008;

Object System
  with
    audioEnabled 0,
    audioExt AUDIOEXT_NULL,
    codeNameLinkId 0,
    currentBackgroundLoop 0,
    currentCmdSupportsPlayback 1,
    previouslyLooked 0,
    previousExaminedItem 0,
    currentCommandWord 0,
    currentCommandObject 0,
    promptIsClickable 0,
    waitingForEnter 0,
    clickedContextSensitiveLink false,
    SetCurrentBackgroundLoop [ f;
      self.currentBackgroundLoop = f;
    ],
    FlushBinOrTTSIfNarrationEnabled [;
      if (self.currentCmdSupportsPlayback ~= 1) {
        VorpleExecuteJavaScriptCommand("CancelBin();");
      }
      if (self.audioEnabled) {
        VorpleExecuteJavaScriptCommand("FlushBinOrTTS();");
      }
    ],
    RestartBackgroundLoop [ f delay;
      VorpleStopSoundEffects();
      if (self.audioEnabled) {
        if (f == 0 && currentBackgroundLoop ~= 0)
          f = self.currentBackgroundLoop;
        if (f ~= 0)
          VorplePlaySoundEffect(f,true,delay);
      }
    ],
    SyncBrowserAudioExtension [;
      switch (self.audioExt) {
        AUDIOEXT_OGG:
          VorpleExecuteJavaScriptCommand("document.audioExt = 'ogg';");
        AUDIOEXT_MP3:
          VorpleExecuteJavaScriptCommand("document.audioExt = 'mp3';");
        default:
          VorpleExecuteJavaScriptCommand("document.audioExt = null;");
      }
    ],
    SyncSystemAudioExtension [ tmp;
      VorpleExecuteJavaScriptCommand("return IsPlayingMP3s();");
      tmp = VorpleWhatNumberWasReturned();
      if (tmp == 1) {
        self.audioExt = AUDIOEXT_MP3;
      }
      else {
        VorpleExecuteJavaScriptCommand("return IsPlayingOGGs();");
        tmp = VorpleWhatNumberWasReturned();
        if (tmp == 1) {
          self.audioExt = AUDIOEXT_OGG;
        }
        else {
          self.audioExt = AUDIOEXT_NULL;
        }
      }
    ],
    StandardPrompt [;
      VorpleExecuteJavaScriptCommand("vorple.prompt.setPrefix('>')");
      bp_output_stream(3, current_prompt, BUFLEN-1);
      print ">";
      bp_output_stream(-3);
    ],
    BlankPrompt [;
      VorpleExecuteJavaScriptCommand("vorple.prompt.setPrefix('')");
      bp_output_stream(3, current_prompt, BUFLEN-1);
      print "";
      bp_output_stream(-3);
    ],
    ClearNonIpadScreen [ ipad;
      if (isVorpleSupported()) {
        VorpleExecuteJavaScriptCommand("return IsIpad();");
        ipad = VorpleWhatNumberWasReturned();
        if (ipad == 0) {
          VorpleExecuteJavaScriptCommand("$('#window0 *:not(.turn.current), .turn.current').empty()");
        }
      }
      else {
        ClearScreen();
      }
    ],
    MsgYesOrNo [;
      print "Please answer ";
      self.InlineLink("Yes",LINKID_YES);
      print " or ";
      self.InlineLink("No",LINKID_NO);
      print ".";
    ],
    InlineLink [ text id;
      if (isVorpleSupported()) {
        VorpleLinkCommand(text);
      }
      else if (glk_gestalt(gestalt_Hyperlinks, 0) && id > 0) {
        glk_set_hyperlink(id);
        PrintStringOrArray(text);
        glk_set_hyperlink(0);
      }
      else {
        PrintStringOrArray(text);
      }
    ],
    InlineLinkText [ cmd text id;
      if (isVorpleSupported()) {
        VorpleLinkCommandText(cmd, text);
      }
      else if (glk_gestalt(gestalt_Hyperlinks, 0) && id > 0) {
        glk_set_hyperlink(id);
        PrintStringOrArray(text);
        glk_set_hyperlink(0);
      }
      else {
        PrintStringOrArray(text);
      }
    ],
    InlineContextualCommand [ id;
      bp_output_stream(3, shortstr, LEN_SHORTSTR);
      print (address)self.currentCommandWord;
      bp_output_stream(-3);

      bp_output_stream(3, hugehugestr, LEN_HUGEHUGESTR);
      print (address)self.currentCommandWord;
      print " ";
      self.currentCommandObject.short_name();
      bp_output_stream(-3);

      if (isVorpleSupported()) {
        self.InlineLinkText(VorpleEscape(hugehugestr), shortstr);
        print " ";
      }
      else if (glk_gestalt(gestalt_Hyperlinks, 0) && id > 0) {
        glk_set_hyperlink(id);
        print (address)self.currentCommandWord;
        glk_set_hyperlink(0);
        print " ";
      }
      else {
        print (address)self.currentCommandWord;
        print " ";
      }
    ],
    ClickablePrompt [ id classes;
      if (isVorpleSupported()) {
        print ">";
        VorpleExecuteJavaScriptCommand("document.lineInputPrefixElement.innerHTML = '<a onclick=~vorple.prompt.queueCommand(document.menuCommand);~>&gt;</a>';");
      }
      else {
        glk_set_hyperlink(LINKID_MENU);
        print ">";
        glk_set_hyperlink(0);
      }
    ],
    WaitForEnter [;
      System.waitingForEnter = 1;
      VorpleExecuteJavaScriptCommand("vorple.prompt.setPrefix('')");
      KeyboardPrimitive(buffer, parse);
      VorpleExecuteJavaScriptCommand("vorple.prompt.setPrefix('>')");
      System.waitingForEnter = 0;
      return parse;
    ],
    Pause [;
      if (isVorpleSupported()) {
        VorpleImage("continue.png", "CLICK TO CONTINUE", IMAGE_RIGHT_ALIGNED, 0, "");
      }
      else {
        glk_set_hyperlink(LINKID_CONTINUE);
        print (markup) "<IMG SRC=101 ALIGN='RIGHT' ALT='CLICK TO CONTINUE' WIDTH=240 HEIGHT=43><br>";
        glk_set_hyperlink(0);
      }
      self.WaitForEnter();
      VorpleExecuteJavaScriptCommand("try { document.getElementById(document.currentContinueId).style.display=~none~ } catch(e) {};");
    ],
    GetCodename [ raw;
      switch (self.codeNameLinkId) {
        LINKID_HERO: if (raw) return CODENAME_HERO; return "Hero";
        LINKID_HEROINE: if (raw) return CODENAME_HEROINE; return "Heroine";
        LINKID_HERETIC: if (raw) return CODENAME_HERETIC; return "Heretic";
        LINKID_HELLION: if (raw) return CODENAME_HELLION; return "Hellion";
        LINKID_HONEYBADGER: if (raw) return CODENAME_HONEYBADGER; return "Honeybadger";
        default: if (raw) return CODENAME_HACKER; return "Hacker";
      }
    ],
    GraceNote [ channel;
      if (isVorpleSupported()) {
        VorpleImage("gracenotes.png", "MUSICAL INTERLUDE");
      }
      else {
        print (markup) "<IMG SRC=100 ALT='MUSICAL INTERLUDE' WIDTH=500 HEIGHT=30><br>";
      }
    ];

[ Initialise o;
    location = clearing;
    inventory_style = FULLINV_BIT + ENGLISH_BIT;
    no_implicit_actions = true;
    glk_request_hyperlink_event(gg_mainwin);
    VorpleInitialise();
    VorpleStopAudio();

    objectloop(o provides articles) {
      if (isVorpleSupported() == 0) {
        o.&articles-->0 = "The ";
        o.&articles-->1 = "the ";
        o.&articles-->2 = "a ";
      }
    }

    move satchel to player;
    move cloak to player;
    move harness to player;
    player.add_to_scope = PlayerAddToScope;
];

Class HyperLinkedObject
  with
    GetContextSensitiveLinkID [cmd;
      switch (cmd) {
        'inspect': return CSLINKID_INSPECT;
        'take': return CSLINKID_TAKE;
        'drop': return CSLINKID_DROP;
        'open': return CSLINKID_OPEN;
        'close': return CSLINKID_CLOSE;
        'wear': return CSLINKID_WEAR;
        'remove': return CSLINKID_REMOVE;
        'empty': return CSLINKID_EMPTY;
        default: return 0;
      }
    ],
    GetContextSensitiveCommandWord [id;
      switch (id) {
        CSLINKID_INSPECT: return 'inspect';
        CSLINKID_TAKE: return 'take';
        CSLINKID_DROP: return 'drop';
        CSLINKID_OPEN: return 'open';
        CSLINKID_CLOSE: return 'close';
        CSLINKID_WEAR: return 'wear';
        CSLINKID_REMOVE: return 'remove';
        CSLINKID_EMPTY: return 'empty';
        default: return 0;
      }
    ],
    ListCommands [ ix it;
      if (self provides basic_commands) {
        for (ix = 0: ix < (self.#basic_commands/4): ix++) {
          it++;
          if (it > 1 && ix < (self.#basic_commands/4)) {
            print ", ";
          }
          System.currentCommandWord = self.&basic_commands-->ix;
          System.currentCommandObject = noun;
          System.InlineContextualCommand(self.GetContextSensitiveLinkID(self.&basic_commands-->ix));
          System.currentCommandWord = 0;
        }
      }
      if (self provides open_commands) {
        for (ix = 0: ix < (self.#open_commands/4): ix++) {
          it++;
          if (it > 1 && ix < (self.#open_commands/4)) {
            print ", ";
          }
          System.currentCommandWord = self.&open_commands-->ix;
          System.currentCommandObject = noun;
          System.InlineContextualCommand(self.GetContextSensitiveLinkID(self.&open_commands-->ix));
          System.currentCommandWord = 0;
        }
      }
      if (self provides wear_commands) {
        for (ix = 0: ix < (self.#wear_commands/4): ix++) {
          it++;
          if (it > 1 && ix < (self.#wear_commands/4)) {
            print ", ";
          }
          System.currentCommandWord = self.&wear_commands-->ix;
          System.currentCommandObject = noun;
          System.InlineContextualCommand(self.GetContextSensitiveLinkID(self.&wear_commands-->ix));
          System.currentCommandWord = 0;
        }
      }
      if (self provides container_commands) {
        for (ix = 0: ix < (self.#container_commands/4): ix++) {
          it++;
          if (it > 1 && ix < (self.#container_commands/4)) {
            print ", ";
          }
          System.currentCommandWord = self.&container_commands-->ix;
          System.currentCommandObject = noun;
          System.InlineContextualCommand(self.GetContextSensitiveLinkID(self.&container_commands-->ix));
          System.currentCommandWord = 0;
        }
      }
    ];

Class HyperLinkedItem
  class HyperLinkedObject,
  with
    basic_commands 'inspect' 'take' 'drop';

Class HyperLinkedWearable
  class HyperLinkedObject,
  with
    wear_commands 'wear' 'remove';

Class HyperLinkedOpenable
  class HyperLinkedObject,
  with
    open_commands 'open' 'close';

Class HyperLinkedContainer
  class HyperLinkedObject,
  with
    container_commands 'empty';

Class HyperLinkedContextQueues
  class HyperLinkedObject;

[ TestMode;
  System.audioEnabled = 1;
  System.SyncSystemAudioExtension();
  System.codeNameLinkId = LINKID_HERO;
  System.SetCurrentBackgroundLoop("music_loop_1");
  ClearNarration();
];

[ InitialQuery tmp;
  print "^A twilight mission to foment revolution awaits you, but some mediocre musician won't pipe down or stay down! Good thing foul play is your forte...^^";

  VorpleStyle(STYLE_EMPHASIS);
  print (markup) "<U>DIRECTOR'S NOTE:^This game supports clickable command words highlighted in this ";
  System.InlineLink("Color",LINKID_COLOR);
  print (markup) " as well as traditional typed commands.</U>^^";
  VorpleEndStyle();
  print "Please enter or select ";
  System.InlineLink("New",LINKID_NEW);
  print " to start a new game, or ";
  System.InlineLink("Restore",LINKID_RESTORE);
  print (markup) " to resume a previously saved session.</U>";
  tmp = 0;
  while (tmp == 0) {
    Keyboard(buffer, parse);
    StringToLower(buffer);
    if (CompareStrings(buffer,INPUT_COLOR)) {
      print "Don't be silly.  ";
      System.InlineLink("Color",LINKID_COLOR);
      print " is just an example, not a valid command.^";
    }
    if (CompareStrings(buffer,INPUT_RESTORE) || CompareStrings(buffer,INPUT_NEW)) {
      if (CompareStrings(buffer,INPUT_RESTORE)) {
        tmp = 0;
        RestoreSub();
        print "^Please enter ";
        System.InlineLink("New",LINKID_NEW);
        print " to start a new game, or ";
        System.InlineLink("Restore",LINKID_RESTORE);
        print " to resume a previously saved session.^";
      }
      else {
        tmp = 1;
      }
    }
    else {
      print "^Please enter ";
      System.InlineLink("New",LINKID_NEW);
      print " to start a new game, or ";
      System.InlineLink("Restore",LINKID_RESTORE);
      print " to resume a previously saved session.^";
    }
  }
];

[ InteractiveIntroduction tmp v key;
  key = 0;
  System.StandardPrompt();
  if (isVorpleSupported()) {
    VorpleExecuteJavaScriptCommand("return IsTTSSupported();");
    tmp = VorpleWhatNumberWasReturned();
    if (tmp == 1) {
      v = true;
      !FIXME add a compelling and funny first line
      #Ifdef TEST_MODE;
      return TestMode();
      #Endif;
      InitialQuery();
      print "^Would you like to enable audio? ";
      System.MsgYesOrNo();
      System.audioEnabled = YesOrNo();
      if (System.audioEnabled) {
        ClearNarration();
        QueueBinOutput("na_intro_1");
        print "^";
        print "^NARRATOR:^";
        print "Oh dear - such dreadful weather.  Do you hear me speaking right now? (";
        System.InlineLink("Yes",LINKID_YES);
        print " or ";
        System.InlineLink("No",LINKID_NO);
        print ")^";
        System.FlushBinOrTTSIfNarrationEnabled();
        tmp = YesOrNo();
        if (tmp) {
          System.SyncSystemAudioExtension();
        }
        else {
          VorpleExecuteJavaScriptCommand("ToggleAudioExtension();");
          ClearNarration();
          QueueBinOutput("na_intro_1b");
          print "^";
          print "^Oh, how about now then? (";
          System.InlineLink("Yes",LINKID_YES);
          print " or ";
          System.InlineLink("No",LINKID_NO);
          print ")^";
          System.FlushBinOrTTSIfNarrationEnabled();
          tmp = YesOrNo();
          if (tmp) {
            System.SyncSystemAudioExtension();
          }
        }
        if (tmp) {
          ClearNarration();
          QueueBinOutput("na_intro_1c");
          QueueBinOutput("se_pause");
          QueueBinOutput("na_intro_1d");
          QueueBinOutput("se_pause");
          print "^";
          !FIXME implement SOUND ON / SOUND OFF
          print "^During the game, you may toggle the audio with the Sound On or Sound Off commands.  Meanwhile, if you see the continue symbol or hear the report of musical drums...you should press the enter key or tap continue when ready to proceed.^";
          System.FlushBinOrTTSIfNarrationEnabled();
          System.Pause();
          ClearNarration();
          QueueBinOutput("na_intro_2");
          QueueBinOutput("se_pause");
          print "^";
          print "^On occasion, the game may employ miraculous text-to-speech technology to read some words aloud.^";
          System.FlushBinOrTTSIfNarrationEnabled();
          System.Pause();
          ClearNarration();
          print "^This is some sample text-to-speech.  Would you like to customize the voice? (";
          System.InlineLink("Yes",LINKID_YES);
          print " or ";
          System.InlineLink("No",LINKID_NO);
          print ")^";
          System.FlushBinOrTTSIfNarrationEnabled();
          tmp = YesOrNo();
          if (tmp) {
            ClearNarration();
            print "^So would I.  Alas, not today.  Enjoy the game!";
            PunctuateTTS();
            System.FlushBinOrTTSIfNarrationEnabled();
            key = 1;
          }
          else {
            ClearNarration();
            QueueBinOutput("na_intro_3");
            QueueBinOutput("se_pause");
            print "^";
            print "^Excellent.  You are ready to begin.  Enjoy the game!^";
            System.FlushBinOrTTSIfNarrationEnabled();
            key = 1;
          }
        }
        else {
          ClearNarration();
          print "^Never mind about the audio then.  During the game, you may toggle the audio with the Sound On or Sound Off commands.  Meanwhile, if you see the continue symbol you should press the enter key or tap continue when ready to proceed.";
          key = 1;
          System.audioEnabled = 0;
        }
      }
      else {
        print "^During the game, you may toggle the audio with the Sound On or Sound Off commands.  Meanwhile, if you see the continue symbol you should press the enter key or tap continue when ready to proceed.";
        key = 1;
      }
    }
    else {
      v = false;
    }
  }
  else {
    v = false;
  }
  if (v == false) {
    #Ifdef TEST_MODE;
    return TestMode();
    #Endif;
    InitialQuery();

    print "^NARRATOR:^";
    print "Please note that this version of the game has no audio.  When you see the continue symbol, tap it or press enter to proceed.  Enjoy the game!^^";
    key = 1;
  }
  if (key == 1) {
    System.Pause();
  }
  ClearNarration();
  print "^^";
  System.ClearNonIpadScreen();
  VorpleStopAudio();

  !FIXME Improve concision and humor
  if (isVorpleSupported()) {
    VorpleImage("title.png", "The Uninvited Opera Of Augustus Interruptus");
  }
  else {
    print (markup) "<IMG SRC=102 ALT='The Uninvited Opera of Augustus Interruptus'<br>";
    print "^";
  }

  QueueBinOutput("na_prologue_1");
  print "^";
  print "NARRATOR:^";
  print "Your people have known nothing but tyranny for one thousand years.  Healthcare is free, yes, but only for ones who are sick! Pointless war and carnage is long forbidden, blanching the countryside with the sickly pallor of peace and prosperity.  And worst of all, excessive overdue library fines - pennies per day! All that changes tonight, and you shall be the changer!^";
  print "^Yet no one shall ever know your name.  Indeed, if you even have a true name it is lost to time.  You were raised by the Order of Humble Pride, a cabal of freedom fighters masquerading as learned monks.  'Twas twenty-four years ago they found you abandoned at the gates and assigned to you your nom de guerre - a code name otherwise known only to the elders.^";
  print "^What was it again? Something that started with H I believe. ";
  System.InlineLink("Hero",LINKID_HERO);
  print "? ";
  System.InlineLink("Heroine",LINKID_HEROINE);
  print "? ";
  System.InlineLink("Heretic",LINKID_HERETIC);
  print "? ";
  System.InlineLink("Hellion",LINKID_HELLION);
  print "? ";
  print "Or perhaps ";
  System.InlineLink("Honeybadger",LINKID_HONEYBADGER);
  print "? You can tell me.  I'm the narrator, after all.^";
  System.FlushBinOrTTSIfNarrationEnabled();
  if (true) {
    tmp = false;
    while (tmp == false) {
      Keyboard(buffer, parse);
      StringToLower(buffer);

      if (CompareStrings(buffer,CODENAME_HERO) || CompareStrings(buffer,CODENAME_HEROINE)) {
        tmp = true;
      }
      else if (CompareStrings(buffer,CODENAME_HERETIC)) {
        tmp = true;
      }
      else if (CompareStrings(buffer,CODENAME_HELLION)) {
        tmp = true;
      }
      else if (CompareStrings(buffer,CODENAME_HONEYBADGER)) {
        tmp = true;
      }
      else {
        ClearNarration();
        QueueBinOutput("na_prologue_1_rejection");
        print "^No no no, it can't be that.  Please enter ";
        System.InlineLink("Hero",LINKID_HERO);
        print ", ";
        System.InlineLink("Heroine",LINKID_HEROINE);
        print ", ";
        System.InlineLink("Heretic",LINKID_HERETIC);
        print ", ";
        System.InlineLink("Hellion",LINKID_HELLION);
        print ", or ";
        System.InlineLink("Honeybadger",LINKID_HONEYBADGER);
        print ".^";
        System.FlushBinOrTTSIfNarrationEnabled();
      }
    }
    System.SetCurrentBackgroundLoop("music_loop_1");
    System.RestartBackgroundLoop(0,250);
    ClearNarration();
    QueueBinOutput("na_prologue_1_ahyes");
    print "^";
    print "^Ah yes - ";
    if (CompareStrings(buffer,CODENAME_HERO) || CompareStrings(buffer,CODENAME_HEROINE)) {
      if (CompareStrings(buffer,CODENAME_HERO)) {
        System.codeNameLinkId = LINKID_HERO;
        QueueBinOutput("na_prologue_1_hero");
        print "Hero! ";
      }
      else {
        System.codeNameLinkId = LINKID_HEROINE;
        QueueBinOutput("na_prologue_1_heroine");
        print "Heroine! ";
      }
      QueueBinOutput("na_prologue_1_hero_fate");
      QueueBinOutput("na_prologue_1_wait");
      QueueBinOutput("se_pause");
      print "Destined to save your nation and change the world! Wait, I'm sorry.  Do you hear that? It sounds like someone is playing a lute - and poorly, I might add...^";
      System.FlushBinOrTTSIfNarrationEnabled();
    }
    else if (CompareStrings(buffer,CODENAME_HERETIC)) {
      System.codeNameLinkId = LINKID_HERETIC;
      QueueBinOutput("na_prologue_1_heretic");
      QueueBinOutput("na_prologue_1_heretic_fate");
      QueueBinOutput("na_prologue_1_wait");
      QueueBinOutput("se_pause");
      print "Heretic! Ever ready to trample the trappings of fear and falsehood beneath your two bare feet.  Wait, I'm sorry.  Do you hear that? It sounds like someone is playing a lute - and poorly, I might add...^";
      System.FlushBinOrTTSIfNarrationEnabled();
    }
    else if (CompareStrings(buffer,CODENAME_HELLION)) {
      System.codeNameLinkId = LINKID_HELLION;
      QueueBinOutput("na_prologue_1_hellion");
      QueueBinOutput("na_prologue_1_hellion_fate");
      QueueBinOutput("na_prologue_1_wait");
      QueueBinOutput("se_pause");
      print "Hellion! Relentlessly spitting in the face of fickle fate! Wait, I'm sorry.  Do you hear that? It sounds like someone is playing a lute - and poorly, I might add...^";
      System.FlushBinOrTTSIfNarrationEnabled();
    }
    else if (CompareStrings(buffer,CODENAME_HONEYBADGER)) {
      System.codeNameLinkId = LINKID_HONEYBADGER;
      QueueBinOutput("na_prologue_1_honeybadger");
      QueueBinOutput("na_prologue_1_honeybadger_fate");
      QueueBinOutput("na_prologue_1_wait");
      QueueBinOutput("se_pause");
      print "Honeybadger! The soul of a wolverine in the body of a...wolverinish...badger body! Wait, I'm sorry.  Do you hear that? It sounds like someone is playing a lute - and poorly, I might add...^";
      System.FlushBinOrTTSIfNarrationEnabled();
    }
  }
  System.promptIsClickable = 1;
  System.Pause();
  ClearNarration();
  QueueBinOutput("na_prologue_1_conclusion");
  print "^";
  print "^Well, that's something not seen every day.  A songsmith in full dress - flamboyant tunic, feathered cap and all!^";
  QueueBinOutput("as_intro_v1");
  bp_output_stream(3, hugehugestr, LEN_HUGEHUGESTR);
  print "as_intro_v2_";
  PrintStringOrArray(System.GetCodename(true));
  bp_output_stream(-3);
  QueueBinOutput(hugehugestr);
  QueueBinOutput("as_intro_v3_aside");
  if (isVorpleSupported()) {
    print "^";
  }
  print "^";
  print "~Who will fight oppression? Who will save us from our shame?^";
  print "One rises to the challenge! ";
  PrintStringOrArray(System.GetCodeName());
  print " is their name!^";
  print "^";
  print "Hello! Call me Augustus - apprentice songsmith.  Don't mind me, go on about your secret mission.  You won't even know I'm here.~^";
];

[ QueueBinOutput f;
  if (isVorpleSupported()) {
    print "=";
    PrintStringOrArray(f);
    print ";";
  }
];

[ PunctuateTTS;
  if (isVorpleSupported()) {
    print "=!;^";
  }
];

Object clearing "Shadowy Clearing..."
  with
    short_name [;
      bp_output_stream(3, hugehugestr, LEN_HUGEHUGESTR);
      print "na_msg_our_";
      PrintStringOrArray(System.GetCodename(true));
      bp_output_stream(-3);
      QueueBinOutput(hugehugestr);
      QueueBinOutput("na_room_clearing");
      print "Our ";
      PrintStringOrArray(System.GetCodeName());
      print " Lingers in a Shadowy Clearing^";
      return true;
    ],
    description [;
      QueueBinOutput("na_desc_clearing");
      print "Immediately to the ";
      System.InlineLinkText("go south","south",LINKID_SOUTH);
      print ", the ";
      distant_tower.short_name();
      print " rises high above the ";
      trees.short_name();
      "";
    ],
    before [;
      Jump:
        VorpleStopSoundEffects();
        QueueBinOutput("na_msg_jump");
        print "Everything falls silent.^";
        System.FlushBinOrTTSIfNarrationEnabled();
        rtrue;
      Sing:
        QueueBinOutput("na_msg_jump");
        print "Everything falls silent.^";
        System.FlushBinOrTTSIfNarrationEnabled();
        rtrue;
    ],
  has light;

  Object -> trees "trees"
    with
      name 'trees' 'tree',
      short_name [;
        System.InlineLinkText("examine trees", "trees", LINKID_TREES);
        return true;
      ],
      description [;
        print "Just some ";
        self.short_name();
        ".";
      ],
    has scenery;

  Object -> path "path"
    with
      name 'path',
      short_name [;
        System.InlineLinkText("examine path", "path", LINKID_PATH);
        return true;
      ],
      description [;
        print "The only ";
        self.short_name();
        print " through the dense ";
        trees.short_name();
        print " runs ";
        System.InlineLinkText("go south","south",LINKID_SOUTH);
        if (parent(augustus) == parent(path)) {
          if (augustus hasnt general) {
            print ", but ";
            augustus.short_name();
            print " is in the way";
          }
          else {
            print ", and fortunately ";
            augustus.short_name();
            print " is no longer in the way";
          }
        }
        ".";
      ],
      has scenery;

  Object -> distant_tower "outer watch tower"
    with
      name 'outer' 'watch' 'tower',
      short_name [;
        System.InlineLinkText("examine outer watch tower", "outer watch tower", LINKID_OWTOWER);
        return true;
      ],
      description [;
        print "You can just make out a single ";
        distant_watchman.short_name();
        print " standing atop the ";
        self.short_name();
        "";
      ],
    has scenery;

  Object -> distant_watchman "watchman"
    with
      name 'guard' 'watchman',
      short_name [;
        System.InlineLinkText("examine watchman", "watchman", LINKID_WATCHMAN);
        return true;
      ],
      description [;
        print "You cannot see the ";
        self.short_name();
        " very well from here.";
      ],
    has scenery;

  Object -> augustus "Augustus"
    with
      short_name [;
        System.InlineLinkText("examine Augustus", "Augustus", LINKID_AUGUSTUS);
        return true;
      ],
      name 'augustus',
      description "A second rate songsmith in full dress.",
      initial [;
        QueueBinOutput("na_desc_augustus_1");
        self.short_name();
        print " is here attentively playing a ";
        lute.short_name();
        print " and obliviously blocking your ";
        path.short_name();
        ".";
      ],
  has transparent proper animate;

  Object -> -> lute "lute"
    with
      short_name [;
        System.InlineLinkText("examine lute", "lute", LINKID_LUTE);
        return true;
      ],
      name 'lute',
      description [;
        print "It's a ";
        self.short_name();
        ".";
      ];

  Object -> convenience
    with
      state 1,
      output [;
        switch (self.state) {
          1:
             QueueBinOutput("na_convenience_1");
             print "You draw your ";
             cloak.short_name();
             print " close against the ";
             chill.short_name();
             print ".  You adjust your ";
             harness.short_name();
             print " and your ";
             satchel.short_name();
             print " of ";
             supplies.short_name();
             print ".  Time to topple a tyrant.";
             self.state = 0;
        }
      ],
      initial [;
        self.output();
        "";
      ];

Object harness "supple satin lined weapons harness"
  class HyperLinkedItem HyperLinkedWearable HyperLinkedContainer,
  with
    bulky,
    name 'supple' 'satin' 'lined' 'weapons' 'harness',
    articles "=na_name_the_harness;The " "=na_name_the_harness;the " "=na_name_a_harness;a ",
    before [;
      Receive:
        if (~~(noun provides weapon)) {
          print "Your ";
          self.short_name();
          print " will only accomodate your weapons.^";
          return true;
        }
    ],
    short_name [;
      System.InlineLinkText("examine supple satin lined weapons harness", "supple satin lined weapons harness", LINKID_HARNESS);
      return true;
    ],
  has container worn clothing open custom_listing;

  Object -> truncheon "trusty truncheon"
    class HyperLinkedItem,
    with
      bulky,
      weapon,
      name 'trusty' 'truncheon',
      articles "=na_name_the_truncheon;The " "=na_name_the_truncheon;the " "=na_name_a_truncheon;a ",
      short_name [;
        System.InlineLinkText("examine trusty truncheon", "trusty truncheon", LINKID_TRUNCHEON);
        return true;
      ];


  Object -> knife "needle pointed knife"
    class HyperLinkedItem,
    with
      sharp,
      weapon,
      name 'needle' 'pointed' 'knife',
      articles "=na_name_the_knife;The " "=na_name_the_knife;the " "=na_name_a_knife;a ",
      short_name [;
        System.InlineLinkText("examine needle pointed knife", "needle pointed knife", LINKID_KNIFE);
        return true;
      ];


Object cloak "woven woolen cloak"
  class HyperLinkedItem HyperLinkedWearable,
  with
  bulky,
  name 'woven' 'woolen' 'cloak',
  articles "=na_name_the_cloak;The " "=na_name_the_cloak;the " "=na_name_a_cloak;a ",
  short_name [;
    System.InlineLinkText("examine woven woolen cloak", "woven woolen cloak", LINKID_CLOAK);
    return true;
  ],
  after [;
    Disrobe:
      give cloak ~general;
  ],
  has transparent worn clothing custom_listing;

  Object -> hood "hood"
    class HyperLinkedWearable,
    with
      name 'hood',
      articles "=na_name_the_hood;The " "=na_name_the_hood;the " "=na_name_a_hood;a ",
      description [;
        print "It's a ";
        self.short_name();
        if (cloak in player && cloak has worn && cloak has general) {
          print " pulled low over your ";
          face.short_name();
          print " obscuring your features.";
        }
        else if (cloak in player && cloak has worn && cloak hasnt general) {
          print " hanging down around your ";
          shoulders.short_name();
          print " revealing your ";
          face.short_name();
          print " to the world.";
        }
        else {
          print ".";
        }
        print "^";
      ],
      before [;
        Wear:
          if (cloak has general && cloak has worn && cloak in actor) {
            print "You are already wearing the ";
            hood.short_name();
            print ".^";
            return true;
          }
          else if (cloak has worn && cloak in actor) {
            give cloak general;
            print "You pull up the ";
            hood.short_name();
            print " around your ";
            face.short_name();
            print ", obscuring your features.^";
            return true;
          }
          else if (cloak hasnt worn) {
            "You cannot wear the ";
            hood.short_name();
            print " if you are not wearing the ";
            cloak.short_name();
            print "!^";
            return true;
          }
          else if (~~(cloak in actor)) {
            rfalse;
          }
        Disrobe:
          if (cloak hasnt general && cloak has worn && cloak in actor) {
            print "You aren't wearing the ";
            hood.short_name();
            print ".^";
            return true;
          }
          else if (cloak has worn && cloak in actor) {
            give cloak ~general;
            print "You pull down the ";
            hood.short_name();
            print " around your ";
            shoulders.short_name();
            print ", revealing your ";
            face.short_name();
            print " to the world around you.^";
            return true;
          }
          else if (cloak hasnt worn) {
            "You cannot remove the ";
            hood.short_name();
            print " if you are not wearing the ";
            cloak.short_name();
            print "!^";
            return true;
          }
          else if (~~(cloak in actor)) {
            rfalse;
          }
      ],
      short_name [;
        System.InlineLinkText("examine hood", "hood", LINKID_HOOD);
        return true;
      ],
    has scenery;

Object satchel "slim leather satchel"
  class HyperLinkedItem HyperLinkedOpenable HyperLinkedWearable HyperLinkedContainer,
  with
  specialSeal,
  name 'slim' 'leather' 'satchel',
  articles "=na_name_the_satchel;The " "=na_name_the_satchel;the " "=na_name_a_satchel;a ",
  na_article_names "na_name_the_satchel" "na_name_the_satchel" "na_name_a_satchel",
  before [;
    Receive:
      if (noun provides bulky) {
        print "The ";
        noun.short_name();
        print " is too bulky to fit in the ";
        self.short_name();
        print "^";
        return true;
      }
      if (noun provides sharp) {
        print "The ";
        noun.short_name();
        print " would damage the ";
        self.short_name();
        print " if put in there.^";
        return true;
      }

  ],
  short_name [nolink;
    if (nolink) {
      print "slim leather satchel";
      return true;
    }
    System.InlineLinkText("examine slim leather satchel", "slim leather satchel", LINKID_SATCHEL);
    return true;
  ],
  has container openable ~open worn clothing custom_listing;

Object -> pellet "grim green poison pellet"
  class HyperLinkedItem,
  with
    name 'grim' 'green' 'pellet' 'poison',
    articles "=na_name_the_pellet;The " "=na_name_the_pellet;the " "=na_name_a_pellet;a ",
    description "Don't eat it by accident.",
    short_name [;
      System.InlineLinkText("examine grim green poison pellet", "grim green poison pellet", LINKID_PELLET);
      return true;
    ],
  has
    edible;

Object -> card "laminated parchment card"
  class HyperLinkedItem,
  with
    name 'laminated' 'parchment' 'card',
    articles "=na_name_the_card;The " "=na_name_the_card;the " "=na_name_a_card;a ",
    description "It's a license to lie.",
    short_name [;
      System.InlineLinkText("examine laminated parchment card", "laminated parchment card", LINKID_CARD);
      return true;
    ];

Object -> charge "peril powder charge"
  class HyperLinkedItem,
  with
    name 'peril' 'powder' 'charge',
    articles "=na_name_the_charge;The " "=na_name_the_charge;the " "=na_name_a_charge;a ",
    description [;
      "Explosive.";
    ],
    short_name [;
      System.InlineLinkText("examine peril powder charge", "peril powder charge", LINKID_CHARGE);
      return true;
    ];

Object -> vial "glaes faeflask"
  class  VesselClass DelicateClass HyperLinkedItem HyperLinkedOpenable HyperLinkedContainer,
  with
    specialSeal,
    name 'sealed' 'unsealed' 'glaes' 'faeflask' 'flask',
    articles "=na_name_the_vial;The " "=na_name_the_vial;the " "=na_name_a_vial;a ",
    description "Smooth and dark.",
    short_name [;
      System.InlineLinkText("examine glaes faeflask", "glaes faeflask", LINKID_FAEFLASK);
      rtrue;
    ],
  has
    ~open openable;

Object shoulders "shoulders"
  with
    name 'shoulders' 'shoulder',
    description "Broad and strong.  The Order trains you well.",
    parse_name [ w1 w2;
      w1 = NextWord(); w2 = NextWord();
      if(w1 == 'left' && w2 == 'shoulder') return 2;
      if(w1 == 'right' && w2 == 'shoulder') return 2;
      if(w1 == 'shoulder' && w2 == 0) return 1;
      if(w1 == 'shoulders' && w2 == 0) return 1;
    ],
    short_name [;
      System.InlineLinkText("examine my shoulders", "shoulders", LINKID_SHOULDERS);
      return true;
    ],
  has scenery;

Object face "face"
  with
    name 'face',
    description "A smooth and unlined forehead crowns eyes of unquenchable fire.  Or so you've been told.",
    short_name [;
      System.InlineLinkText("examine my face", "face", LINKID_FACE);
      return true;
    ],
  has scenery;

Object body "body"
  with
    name 'body',
    description "Slim, trim and covered in horrific scars that never cease to writhe and itch.",
    short_name [;
      System.InlineLinkText("examine my body", "body", LINKID_BODY);
      return true;
    ],
  has scenery;

Object back "back"
  with
    name 'back',
    description "Still aching from this morning's grueling training session.",
    short_name [;
      System.InlineLinkText("examine my back", "back", LINKID_BACK);
      return true;
    ],
  has scenery;

Object narrator "narrator"
  with
    name 'narrator',
    before [;
      QueueBinOutput("na_msg_narrator_interaction");
      "Don't be silly - you can't interact with me directly.";
    ],
  has scenery;

Object chill "chill"
  with
    name 'chill',
    description [;
      print "It's ";
      System.InlineLinkText("examine chill", "chilly", LINKID_CHILL);
      print ".";
    ],
    before [;
      if (action == ##Examine) {
          return false;
      }
      else {
        "Don't let the cold distract you.  Focus on the task at hand.";
      }
    ],
    short_name [;
      System.InlineLinkText("examine chill", "chill", LINKID_CHILL);
      return true;
    ],
  has scenery;

Object supplies "supplies"
  with
    name 'supplies' 'supply',
    before [;
      if (action == ##Examine) {
          <<UOAIInv>>;
      }
      else {
        print "To interact with a specific supply item, refer to it by name.  The ";
        System.InlineLinkText("inventory", "Inventory", LINKID_INVENTORY);
        print " command will list all of your current ";
        self.short_name();
        ".";
      }
    ],
    short_name [;
      System.InlineLinkText("inventory", "supplies", LINKID_INVENTORY);
      return true;
    ],
  has scenery;

Object room "Cell"
  with
    description [;
      QueueBinOutput("na_desc_cell");
      QueueBinOutput("0_25_silence");

      "You are standing in a cell.  The only way out is down.";
    ],
    before [;
      Go:
        if (noun == d_obj) {
          if (System.audioEnabled) {
            VorpleExecuteJavaScriptCommand("PlayNowAsList('na_msg_game_over');");
          }
          print "Indeed, for you will not go gently into that good night.  Game over, for now.";
          KeyCharPrimitive();
          quit;
        }
    ],
  has light;

Object -> pistol "strange pistol"
  with
    name 'strange' 'pistol',
    articles "=na_name_the_pistol;The " "=na_name_the_pistol;the " "=na_name_a_pistol;a ",
    short_name "strange pistol",
    description [;
      QueueBinOutput("na_desc_pistol");
      "There's no trigger.  You can't tell how to fire it.";
    ],
  has clothing;
